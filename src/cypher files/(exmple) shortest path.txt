//MATCH (n) OPTIONAL MATCH (n)-[r]-() DELETE n, r

CREATE
(A:Place{name:'PlaceA'}),(B:Place{name:'PlaceB'}),(D:Place{name:'PlaceD'}),
(E:Place{name:'PlaceE'}),(F:Place{name:'PlaceF'}),(C:Place{name:'PlaceC'}),
(G:Place{name:'PlaceG'}),(H:Place{name:'PlaceH'}),

(A)-[:ROAD{km:1.4142,cost:0}]->(D),(D)-[:ROAD{km:1.4142,cost:0}]->(A),
(A)-[:ROAD{km:1.4142,cost:0}]->(B),(B)-[:ROAD{km:1.4142,cost:0}]->(A),
(A)-[:ROAD{km:1.4142,cost:0}]->(G),(G)-[:ROAD{km:1.4142,cost:0}]->(A),
(D)-[:ROAD{km:1.4142,cost:0}]->(C),(C)-[:ROAD{km:1.4142,cost:0}]->(D),
(B)-[:ROAD{km:1.4142,cost:0}]->(E),(E)-[:ROAD{km:1.4142,cost:0}]->(B),
(H)-[:ROAD{km:1.4142,cost:0}]->(G),(G)-[:ROAD{km:1.4142,cost:0}]->(H),
(H)-[:ROAD{km:10,cost:0}]->(F),(F)-[:ROAD{km:10,cost:0}]->(H),
(E)-[:ROAD{km:5,cost:5}]->(F),(F)-[:ROAD{km:5,cost:5}]->(E),
(F)-[:ROAD{km:1,cost:10}]->(C),(C)-[:ROAD{km:1,cost:10}]->(F)


//ALL unique paths with no backtracking
MATCH  p=(src:Place { name:'PlaceA'})-[:ROAD*1..4]->(dest:Place { name:'PlaceF'})
WHERE ALL(n in nodes(p) WHERE 
          1=size(filter(m in nodes(p) WHERE m=n)))
RETURN p AS path

//Find Literally Any Path
MATCH  p=(src:Place { name:'PlaceA'})-[:ROAD*1..4]->(dest:Place { name:'PlaceF'})
WHERE ALL(n in nodes(p) WHERE 
          1=size(filter(m in nodes(p) WHERE m=n)))
RETURN p AS path
       LIMIT 1

//Minimum Distance Path
MATCH  p=(src:Place { name:'PlaceA'})-[:ROAD*1..4]->(dest:Place { name:'PlaceF'})
WHERE ALL(n in nodes(p) WHERE 
          1=size(filter(m in nodes(p) WHERE m=n)))
RETURN p AS path 
       ,reduce(km=0, r in relationships(p) | km+r.km) AS totaldistance
       ORDER BY totaldistance ASC
       LIMIT 1
	   
//Paths WITHOUT a node (second shortest)
MATCH  p=(src:Place { name:'PlaceA'})-[:ROAD*1..4]->(dest:Place { name:'PlaceF'})
,(exclude1:Place { name:'PlaceC' })
WHERE ALL(n in nodes(p) WHERE 
          1=size(filter(m in nodes(p) WHERE m=n)))
AND NONE (n IN nodes(p) WHERE n=exclude1)
RETURN p AS path, 
       reduce(km=0, r in relationships(p) | km+r.km) AS totaldistance
       ORDER BY totaldistance ASC
       LIMIT 1
	   
//Paths WITH a node
MATCH  p=(src:Place { name:'PlaceA'})-[:ROAD*1..4]->(dest:Place { name:'PlaceF'})
,(include1:Place { name:'PlaceH' })
WHERE ALL(n in nodes(p) WHERE 
          1=size(filter(m in nodes(p) WHERE m=n)))
AND (n IN nodes(p) WHERE n=include1)
RETURN p AS path, 
       reduce(km=0, r in relationships(p) | km+r.km) AS totaldistance
       ORDER BY totaldistance ASC
       LIMIT 1


