options {
	LOOKAHEAD=1;
}

PARSER_BEGIN(ParserNav)

import java.io.*;
import java.util.*;

public class ParserNav {
	
	public static LinkedList<Path> querries = new LinkedList<Path>();
	
	public static void main(String args[]) throws ParseException, IOException {
		
		// checks args
		if(args.length != 1){
			System.out.println("Incorrect arguments, correct use:");
			System.out.println("ParserNav <path of file to be compiled>");
			System.exit(-1);
		}
		
		FileInputStream fis = new FileInputStream(new File(args[0]));
		
		ParserNav parserNav = new ParserNav(fis);
		SimpleNode root = parserNav.ParseExpression();
		
		System.out.println("\n\nAST:\n");
		root.dump("");
		System.out.println("\n--------");
		
		exportToFile(root, args[0]);
	}
	
	public static void exportToFile(SimpleNode root, String filePath) throws IOException{
		System.out.println("Exporting To File\n");
		
		int index = filePath.lastIndexOf(".");
		String exportFile = filePath.substring(0, index);
		exportFile += "_output";
		exportFile += filePath.substring(index, filePath.length());
		
		BufferedWriter bw = new BufferedWriter(new OutputStreamWriter(new FileOutputStream(new File(exportFile))));
		
		int indexAST = 0;
		while(!querries.isEmpty()){
			Path querry = querries.remove();
			
			switch (((SimpleNode) root.jjtGetChild(indexAST)).id) {
			case ParserNavTreeConstants.JJTTO:
				bw.write(((To) querry).toString());
				bw.newLine();
				bw.newLine();
				
				System.out.println((To) querry);
				System.out.println("\n");
				break;
			case ParserNavTreeConstants.JJTCYCLE:
				bw.write(((Cycle) querry).toString());
				bw.newLine();
				bw.newLine();
				
				System.out.println((Cycle) querry);
				System.out.println("\n");
				break;
			case ParserNavTreeConstants.JJTSIMPLECYCLE:
				bw.write(((Cycle) querry).toString());
				bw.newLine();
				bw.newLine();
				
				System.out.println((Cycle) querry);
				System.out.println("\n");
				break;
			}
			
			indexAST++;
		}
		
		bw.close();
	}
}

PARSER_END(ParserNav)

SKIP :
{
  " "
| "\t"
| "\n"
| "\r"
| "\f"
}

TOKEN :
{
	< COMMA: "," >
|	< SEMICOLON: ( ";" ) >
|	< SRC: ( "src" ) >
|	< DEST: ( "dest" ) >
|	< FIND: ( "find" ) >
|	< CYCLE: ( "cycle" ) >
|	< SIMPLECYCLE: ( "simplecycle" ) >
|	< NOT: ( "not" ) >
|	< PASS: ( "pass" ) >
|	< MINLENGTH: ( "minlength" ) >
|	< NEAREST: ( "nearest" ) >
|	< MAXIMIZE: ( "maximize" ) >
|	< MINIMIZE: ( "minimize" ) >
|	< VAR: ["A"-"Z","a"-"z"](["0"-"9","A"-"Z","a"-"z"])* >
|	< INT: (["0"-"9"])+>
}

SimpleNode ParseExpression() #Parse: {Path obj;} {
	ParseExpressionAux()
	{
		return jjtThis;
	}
}

void ParseExpressionAux() #void: {Path obj;} {
	obj=StartNode() <SEMICOLON>
	{
		querries.add(obj);
	}
	( <EOF> | ParseExpressionAux() )
}

Path StartNode() #void: {Token t; Path obj;} {
	<SRC> t=<VAR>
	(obj=Path(t) | obj=Cycle(t) )
	{
		return obj;
	}
}

To Path(Token t) #To: {To obj;} {
	{
		obj = new To();
		obj.sourceNodeName = t.image;
	}
	DestNode(obj) PathCriteria(obj)
	{
		return obj;
	}
}

void DestNode(To obj) #void: {Token t;} {
	<DEST> t=<VAR>
	{
		obj.destNodeName = t.image;
	}
}

void PathCriteria(To obj) #void: {} {
	OptimalCriteria(obj) (RestrictionPath(obj))?
}

void OptimalCriteria(Path obj) #void: {} {
	( <MINIMIZE>
	{
		obj.toMinimize = true;
	}
	| <MAXIMIZE>
	{
		obj.toMinimize = false;
	}
	) Property(obj) 
}

void Property(Path obj) #void: {Token t;} {
	t=<VAR>
	{
		obj.attributeFocus = t.image;
	}
}

void RestrictionPath(Path obj) #void: {} {
	{
		obj.usePassingRestriction = true;
	}
	(<NOT>
	{
		obj.toPass = false;
	}
	)? <PASS>	RestrictionPathParam(obj)
}

void RestrictionPathParam(Path obj) #void: {Token t;} {
	t=<VAR>
	{
		obj.restrictionPathNodes.add(t.image);
	}
	(<COMMA> RestrictionPathParam(obj))?
}

Cycle Cycle(Token t) #void: {Cycle obj;} {
	{
		obj = new Cycle();
		obj.sourceNodeName = t.image;
	}
	CycleCondition(obj) CycleCriteria(obj)
	{
		return obj;
	}
}

void CycleCondition(Cycle obj) #void: {} {
	( <CYCLE> #Cycle
	{
		obj.canRepeatNodes = false;
	}
	| <SIMPLECYCLE> #SimpleCycle
	{
		obj.canRepeatNodes = true;
	}
	)
}

void CycleCriteria(Cycle obj) #void: {} {
	Length(obj) Property(obj) (RestrictionPath(obj))?
}

void Length(Cycle obj) #void: {Token t;} {
	<MINLENGTH> t=<INT>
	{
		obj.minLength = Integer.parseInt(t.image);
	}
}